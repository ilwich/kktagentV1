; Script generated by the Inno Setup Script Wizard.
; SEE THE DOCUMENTATION FOR DETAILS ON CREATING INNO SETUP SCRIPT FILES!

#define MyAppName "KKTAgent"
#define MyAppNameConfig "KKTAgentConfig"
#define MyAppVersion "1.0"
#define MyAppPublisher "5element35, LLC."
#define MyAppURL "https://www.kassbot.website/"
#define MyAppExeName "kktagent.exe"
#define MyAppConfigExeName "kktagentconfig.exe"


[Setup]
; NOTE: The value of AppId uniquely identifies this application. Do not use the same AppId value in installers for other applications.
; (To generate a new GUID, click Tools | Generate GUID inside the IDE.)
AppId={{79A7CB15-607E-448A-914E-1BAF6160D880}
AppName={#MyAppName}
AppVersion={#MyAppVersion}
;AppVerName={#MyAppName} {#MyAppVersion}
AppPublisher={#MyAppPublisher}
AppPublisherURL={#MyAppURL}
AppSupportURL={#MyAppURL}
AppUpdatesURL={#MyAppURL}
DefaultDirName={autopf}\{#MyAppName}
DefaultGroupName={#MyAppName}
DisableProgramGroupPage=yes
LicenseFile=C:\projects\kktagentV2\kktagentV1\dist\license.txt
InfoBeforeFile=C:\projects\kktagentV2\kktagentV1\dist\beforeinstall.txt
InfoAfterFile=C:\projects\kktagentV2\kktagentV1\dist\afterinstall.txt
; Uncomment the following line to run in non administrative install mode (install for current user only.)
PrivilegesRequired=admin
OutputDir=C:\projects\kktagentV2\kktagentV1\msi_32x
OutputBaseFilename=kktagentsetupp_win32
SetupIconFile=C:\projects\kktagentV2\kktagentV1\dist\Ebiene-E-Commerce-Cash-register.ico
Compression=lzma
SolidCompression=yes
WizardStyle=modern

[Languages]
Name: "russian"; MessagesFile: "compiler:Languages\Russian.isl"

[Files]
Source: "C:\projects\kktagentV2\kktagentV1\dist_32x\{#MyAppExeName}"; DestDir: "{app}"; Flags: ignoreversion
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\Kktagentconfig.exe"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\Kktagentconfig.pdb"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\D3DCompiler_47_cor3.dll"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\PenImc_cor3.dll"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\PresentationNative_cor3.dll"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\vcruntime140_cor3.dll"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\public\wpfgfx_cor3.dll"; DestDir: "{app}";
Source: "C:\projects\kktagentV2\kktagentV1\dist\Kktagent.ico"; DestDir: "{app}";
Source: "C:\ProgramData\V_element\kktagent\.secrets.toml"; DestDir: "C:\ProgramData\V_element\kktagent\"
Source: "C:\ProgramData\V_element\kktagent\settings.json"; DestDir: "C:\ProgramData\V_element\kktagent\"
Source: "C:\ProgramData\V_element\kktagent\settings.toml"; DestDir: "C:\ProgramData\V_element\kktagent\"

; NOTE: Don't use "Flags: ignoreversion" on any shared system files

[Icons]
Name: "{group}\{#MyAppName}"; Filename: "{app}\{#MyAppExeName}"
Name: "{userdesktop}\{#MyAppNameConfig}"; Filename: "{app}\{#MyAppConfigExeName}"; \
  AfterInstall: SetElevationBit('{userdesktop}\{#MyAppNameConfig}.lnk'); IconFilename: "{app}\kktagent.ico"

[Run]
Filename: "{app}\{#MyAppExeName}"; Parameters: "--startup=auto install";  WorkingDir: "{app}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: runhidden
Filename: "{app}\{#MyAppExeName}"; Parameters: "start";  WorkingDir: "{app}"; Description: "{cm:LaunchProgram,{#StringChange(MyAppName, '&', '&&')}}"; Flags: runhidden

[UninstallRun]
Filename: "{app}\{#MyAppExeName}"; Parameters: "remove";  WorkingDir: "{app}";

[Code]

procedure SetElevationBit(Filename: string);
var
  Buffer: string;
  Stream: TStream;
begin
  Filename := ExpandConstant(Filename);
  Log('Setting elevation bit for ' + Filename);

  Stream := TFileStream.Create(FileName, fmOpenReadWrite);
  try
    Stream.Seek(21, soFromBeginning);
    SetLength(Buffer, 1);
    Stream.ReadBuffer(Buffer, 1);
    Buffer[1] := Chr(Ord(Buffer[1]) or $20);
    Stream.Seek(-1, soFromCurrent);
    Stream.WriteBuffer(Buffer, 1);
  finally
    Stream.Free;
  end;
end;